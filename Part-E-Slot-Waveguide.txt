# Creating a Slot Waveguide using Lumerical
# MIT 6.S976: Silicon Photonics
# Created by Shoumik Chowdhury (@shoumikdc)

switchtolayout;
deleteall;
clear;

### Create SiO2 cladding ###
addrect;
set("name", "cladding");
set("x", 0);
set("x span", 5e-6); # length: 5µm
set("y", 0);
set("y span", 5e-6); # width: 5µm
set("z", 0);
set("z span", 5e-6); # height: 5µm
set("material", "SiO2 (Glass) - Palik");
set("render type", "wireframe");

### Create left Si core ###
addrect;
set("name", "left core");
set("x", 0);
set("x span", 5e-6); # length: 5µm
set("y min", -0.26e-6); # Rail width 0.2µm
set("y max", -0.06e-6); # Gap width 0.12µm / 2
set("z", 0);
set("z span", 0.22e-6); # height: 0.22µm
set("material", "Si (Silicon) - Palik");

### Create right Si core ###
addrect;
set("name", "right core");
set("x", 0);
set("x span", 5e-6); # length: 5µm
set("y min", 0.06e-6); # Gap width 0.12µm / 2
set("y max", 0.26e-6); # Rail width 0.2µm
set("z", 0);
set("z span", 0.22e-6); # height: 0.22µm
set("material", "Si (Silicon) - Palik");

### Set up Eigenmode Analysis ###
# Set solver dimensions
addfde;
set("solver type", "2D X normal");
set("y", 0);
set("y span", 5e-6);
set("z", 0);
set("z span", 4e-6);

# Meshing
set("mesh cells y", 1000);
set("mesh cells z", 1000);

# Solver settings
set("wavelength", 1.55e-6); # Wavelength: 1550nm
set('number of trial modes', 4);
run;
findmodes;
